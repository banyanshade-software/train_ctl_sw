
#


%prefix "cnfparse"

%value "config_node_t *"
%auxil "system_t *"

%header {
#include <stdio.h>
#include <stdlib.h>
#include "system.h"

}

%source{
#define PCC_ERROR(auxil) system__handle_syntax_error(auxil, SYNTAX_ERROR_UNKNOWN, range__void()) /* <-- caused by incompleteness of the grammar */
#define PCC_GETCHAR(auxil) system__read_source_file(auxil)
#define PCC_MALLOC(auxil, size) system__allocate_memory(auxil, size)
#define PCC_REALLOC(auxil, ptr, size) system__reallocate_memory(auxil, ptr, size)
#define PCC_FREE(auxil, ptr) system__deallocate_memory(auxil, ptr)
 
static const char *dbg_str[] = { "EVAL_", "MATCH", "ABAND" };

#define PCC_DEBUG(auxil, event, rule, level, pos, buffer, length) do {\
     if ((0)) fprintf(stdout, "%*s%s %s @%d [%.*s]\n", (int)(level * 2), "", dbg_str[event], rule, (int)pos, (int)length,  buffer); fflush(stdout); \
} while(0)


}


defall 
    <- _ s:defstatement _ l:defall
        { if (s) {
            $$ = s; 
            $$->next = l;
          } else {
            $$ = l;
          }
          if ($$) $$->range = range__new($0s, $0e);
        }
     / _ s:defstatement
        { $$ = s; }
     / _ end_of_file

defstatement
    <- s:configdef
        { $$ = s; }
     / s:subconfigdef
        { $$ = s; }
     / s:tabledef
        { $$ = s; }
     / _ g:globalattrib
        { $$ = g; }
     / _ a:configattrib
        { $$ = a; }

# ---------------- attribs

globalattrib 
    <- attr_hcode _ '{' <accol*> '}'
      { $$ = create_config_node_string(auxil, CONFIG_NODE_ATTR_CODE, $1); $$->value = 1; }
     / attr_ccode _ '{' <accol*> '}'
      { $$ = create_config_node_string(auxil, CONFIG_NODE_ATTR_CODE, $2); $$->value = 2; }

configattrib
    <- kw_attrib _ i:identifier _ g:globalattrib
      { $$ = create_config_node(auxil, CONFIG_NODE_ATTR_LOCAL, range__new($0s, $0e));
        $$->string = i->string;
        $$->attrnode = g;
      }
     / kw_attrib _ i:identifier _ attr_storetype _ '=' _ t:storetypevalue
      { $$ = create_config_node(auxil, CONFIG_NODE_ATTR_LOCAL, range__new($0s, $0e));
        $$->string = i->string;
        $$->attrnode = t;
      }
     / kw_attrib _ i:identifier _ attr_storenum _ '=' _ n:integer
      { $$ = create_config_node(auxil, CONFIG_NODE_ATTR_LOCAL, range__new($0s, $0e));
        $$->string = i->string;
        config_node_t *a = create_config_node(auxil, CONFIG_NODE_ATTR_STORENUM, range__new($0s, $0e)); 
        a->value = n->value; 
        $$->attrnode = a;
      }

storetypevalue
    <- 'normal' !(L/D)
        { $$ = create_config_node(auxil, CONFIG_NODE_ATTR_STORETYPE, range__new($0s, $0e)); $$->value = 0; }
     / 'local'   !(L/D)
        { $$ = create_config_node(auxil, CONFIG_NODE_ATTR_STORETYPE, range__new($0s, $0e)); $$->value = 1; }



# ---------------- tables
tabledef
    <- kw_table _ n:identifier _ c:tableline  v:tablemultiline _ ';'
        { $$ = create_config_node(auxil, CONFIG_NODE_TABLE, range__new($0s, $0e));
          $$->string = n->string;
          $$->coldef = c;
          $$->lines = v;
        }

tablemultiline
    <- _ a:tableline _ l:tablemultiline
        { $$ = a; a->next = l; }
     / _ a:tableline
        { $$ = a; }

tableline
    <- '(' l:tablevals _ ')'
        { $$ = create_config_node(auxil, CONFIG_NODE_TABLELINE, range__new($0s, $0e));
          $$->lineval = l;
        }

tablevals
    <- _ v:value _ ',' l:tablevals
        { $$ = v; $$->next = l; }
     / _ v:value
        { $$ = v; }

# ---------------- config

configdef
    <- kw_config _ n:identifier _  f:fields  _ kw_num _ b:board_values  _ ';' 
        {
            $$ = create_config_node(auxil, CONFIG_NODE_CONF, range__new($0s, $0e));
            $$->string = n->string;
            $$->fields = f;
            $$->numinst = b;
        }

subconfigdef
    <- kw_subconfig _ n:identifier _  f:fields _ ';'
        {
            $$ = create_config_node(auxil, CONFIG_NODE_SUBCONF, range__new($0s, $0e));
            $$->string = n->string;
            $$->fields = f;
            for (config_node_t *fld = f; fld; fld=fld->next) { fld->parentconf = $$;}
        }


fields
    <- '{' _ l:field_list _ '}'
        { $$ = l; }

field_list 
    <-  f:field _ l:field_list
         { 
            $$ = f;
            $$->next = l;
        }
     /  f:field
        { $$ = f; }

field 
    <- q:qualifier _ t:type _ d:fielddef _ k:board_values _ ';'
        {
            $$ =  create_config_node(auxil, CONFIG_NODE_FIELD, range__new($0s, $0e));
            $$->string = d->string;
            $$->array = d->array;
            $$->bitfield = d->bitfield;
            $$->configurable = q->value;
            $$->nptr = t->value;
            $$->type = t->string;
            $$->boardvalues = k;
        }
     / kw_subref _ v:identifier _ ';'
        {
            $$ = create_config_node(auxil, CONFIG_NODE_SUBREF, range__new($0s, $0e));
            $$->string = strdup(v->string);
        }

fielddef 
    <- i:identifier _ '[' n:integer ']'
        { $$ = i; $$->array = n->value; $$->value = i->value;}
     / i:identifier _ ':' n:integer
        { $$ = i; $$->bitfield = n->value; $$->value = i->value;}
     / i:identifier
        { $$ = i; $$->value = i->value; }

type
    <- i:identifier _ <('*')+>
       { $$ = i; $$->value = $1e-$1s; /*printf("t=%s; nptr=%d\n", $$->string, $$->value);*/}
     / i:identifier
       { $$ = i; }

qualifier
    <- kw_const
        { $$ = create_config_node_int(auxil, CONFIG_NODE_IDENT, range__new($0s, $0e), 0);}
     / kw_user
        { $$ = create_config_node_int(auxil, CONFIG_NODE_IDENT, range__new($0s, $0e), 1);}

board_values 
    <- b:board_val _ l:board_values
       { $$ = b; b->next = l; }
     / b:board_val
       { $$ = b; }

board_val 
    <- '=' _ '(' b:board_id  ')' _ v:value
           { 
            $$ = b; 
            $$->tag = CONFIG_NODE_BOARDVAL; 
            $$->val = v; 
        }

board_id 
    <- i:identifier ':' n:integer
           { $$ = i; $$->value = n->value; }
     / i:identifier ':*'
       { $$ = i; $$->value = -1; }
     / i:identifier
       { $$ = i; $$->value = -1; }

value
    <- kw_table _ '(' _ n:identifier _ ',' _ c:identifier _ ')' 
       { $$ = create_config_node_text(auxil, CONFIG_NODE_TABLEREF, range__new($0s, $0e));
         $$->tablename = strdup(n->string);
         $$->colname = strdup(c->string);
       }
     / v:integer
       { $$ = v; }
     / identifier '(' paren* ')'
       { $$ = create_config_node_text(auxil, CONFIG_NODE_VALUE, range__new($0s, $0e)); }
     / v:identifier
       { $$ = v; }
     / '{' accol* '}'
       { $$ = create_config_node_text(auxil, CONFIG_NODE_VALUE, range__new($0s, $0e)); }

paren 
    <- ![()]. / '(' paren* ')'

accol
    <- ![{}]. / '{' accol* '}'




# ########################################@

integer
   <- '0'[xX]X+
    {
        $$ = create_config_node_intstr(auxil, CONFIG_NODE_INT, range__new($0s, $0e), 16);
    }
    / '0'O+
    {
        $$ = create_config_node_intstr(auxil, CONFIG_NODE_INT, range__new($0s, $0e), 8);
    }
    / !'0' D+
    {
        $$ = create_config_node_intstr(auxil, CONFIG_NODE_INT, range__new($0s, $0e), 10);
    }
    / D !D
    {
        $$ = create_config_node_intstr(auxil, CONFIG_NODE_INT, range__new($0s, $0e), 10);
    }
    / '-'D+
    {
        $$ = create_config_node_intstr(auxil, CONFIG_NODE_INT, range__new($0s, $0e), 10);
    }



kw_config       <- 'config'  !(L/D)
kw_subconfig    <- 'subconfig'  !(L/D)
kw_subref       <- 'sub'  !(L/D)
kw_num          <- 'num' !(L/D)
kw_const        <- 'const' !(L/D)
kw_user         <- 'user' !(L/D)
kw_table        <- 'table' !(L/D)
kw_attrib       <- 'attrib' !(L/D)

attr_hcode       <- 'h_code' !(L/D)
attr_ccode       <- 'c_code' !(L/D)

attr_storetype   <- 'store'  !(L/D)
attr_storenum    <- 'stnum'  !(L/D)


O <- [0-7]
D <- [0-9]
X <- [0-9a-fA-F]
L <- [a-zA-Z_]



keyword
   <- kw_num
    / kw_const
    / kw_user
    / kw_config
    / kw_subconfig
    / kw_subref
    / kw_table
    / kw_attrib
    / attr_hcode
    / attr_ccode

_ <- ( space / comment )*

comment
   <- '/*' ( !'*/' . )* '*/'
    / '#' (!end_of_line .)* end_of_line
    / '//' (!end_of_line .)* end_of_line
    ## error handling ##
    / '/*' ( !'*/' . )*
    {
    fprintf(stderr, "unclosed comment at %ld-%ld", $0s, $0e);
        //system__handle_syntax_error(auxil, SYNTAX_ERROR_UNCLOSED_COMMENT_BLOCK, range__new($0s, $0e));
    }


space <- blank / end_of_line
blank <- [ \t\v\f]
end_of_line <- '\r\n' / '\n' / '\r'
end_of_file <- !.

identifier
   <- !keyword L(L/D)*
    {
        $$ = create_config_node_text(auxil, CONFIG_NODE_IDENT, range__new($0s, $0e));
    }


%%

